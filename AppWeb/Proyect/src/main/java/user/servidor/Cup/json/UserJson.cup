package user.servidor.Cup.json;

import java_cup.runtime.*;
import java.util.ArrayList;
import user.servidor.Errors.ErrorL;
import user.servidor.Objects.*;

class UserJson;

parser code{:
    private ArrayList<User> users = new ArrayList<User>();
    private ArrayList<ErrorL> errors = new ArrayList<ErrorL>();
    private String errorString = "";

    public ArrayList<User> getUsers(){
        return this.users;
    }

    public ArrayList<ErrorL> getErrors(){
        return this.errors;
    }

    public void addError(int line, int column, String message){
        errors.add(new ErrorL(errorString, line, column, "Sintactico", message));
    }

    public void syntax_error(Symbol s){
        errorString = s.value+"";
        String errorString = "Error sintactico recuperable en la linea: "+
        (s.left)+ " columna: " + s.right + ". Componente  " + s.value + " no reconocido.";
        System.out.println("Error sintactico: "+errorString);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        String message = "Error síntactico irrecuperable en la Línea " +
        (s.left)+ " Columna "+s.right+". Componente " + s.value +
        " no reconocido.";
        System.out.println(message);
    }
:}

terminal CORCHETEA, CORCHETEC, KEYA, KEYC, COLON, COMMA, USER, PASSWORD, INSTITUTION, NAME, DATE,
         TEXT, DATECREATION;

non terminal String userStructure, passwordStructure, institutionStructure, nameStructure, dateStructure;

non terminal in, initUser, dataUser, userComponents;

start with in;

in::= initUser;

initUser::= CORCHETEA dataUser CORCHETEC
          | CORCHETEA CORCHETEC
          ;

dataUser::= userComponents COMMA dataUser
          | userComponents
          ;

userComponents::= KEYA
        userStructure:user
        passwordStructure:password
        institutionStructure:institution
        nameStructure:name
        dateStructure:date
        KEYC
        {:
            User userResult = new User(user.toString(), password.toString(), institution.toString(),
            name.toString(), date.toString());
            users.add(userResult);
        :}
        | error:e
          {:
                int line        = (eleft)+1;
                int column     = eright;
                String message  = "No se cumplen los parametros de usuario en tipo json";
                addError(line, column, message);
          :}
        ;

userStructure::= USER COLON TEXT:user COMMA
                {:
                    System.out.println("user" + user.toString());
                    RESULT = user.toString().replace("\"", "");
                :}
                ;

passwordStructure::= PASSWORD COLON TEXT:password COMMA
                    {:
                        RESULT = password.toString().replace("\"", "");
                    :}
                    ;

institutionStructure::= INSTITUTION COLON TEXT:institution COMMA
                        {:
                            RESULT = institution.toString().replace("\"", "");
                        :}
                        ;

nameStructure::= NAME COLON TEXT:name COMMA
                {:
                    RESULT = name.toString().replace("\"", "");
                :}
                ;

dateStructure::= DATE COLON DATECREATION:date
                {:
                    RESULT = date.toString().replace("\"", "");
                :}
                ;